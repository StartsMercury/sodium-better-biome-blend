import static org.gradle.api.JavaVersion.VERSION_17

plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = VERSION_17
targetCompatibility = VERSION_17

repositories {
	maven { url 'https://api.modrinth.com/maven'; content { 'maven.modrinth' } }
	maven { url 'https://cursemaven.com'        ; content { 'curse.maven'    } }
	maven { url 'https://maven.gegy.dev/'                                      }
}

dependencies {
	// Better Biome Blend
	modApi 'curse.maven:better-biome-blend-446253:3615207'
	
	// DataBreaker
	modRuntimeOnly 'supercoder79:databreaker:0.2.8', {
		exclude module: 'fabric-loader'
	}
	
	// Fabric API
	modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	// Fabric Loader
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"

	// Minecraft
	minecraft "com.mojang:minecraft:${minecraft_version}"

	// Official Mojang Mappings
	mappings loom.officialMojangMappings()

	// Sodium
	modApi "maven.modrinth:sodium:mc${minecraft_version}-${sodium_version}"
}

processResources {
	inputs.property 'version', project.version

	filesMatching 'fabric.mod.json', {
		expand 'better_biome_blend_version': project.better_biome_blend_version,
		       'loader_version': project.loader_version,
		       'minecraft_version': project.minecraft_version,
		       'version': project.version,
		       'sodium_version': project.sodium_version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from 'LICENSE', {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava MavenPublication, {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
